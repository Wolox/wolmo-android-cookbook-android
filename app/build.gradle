apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'idea'
apply plugin: 'jacoco-android'
apply from: 'keystore.gradle'
apply from: '../jacoco.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'
apply from: '../scripts/sanity/sanity_task.gradle'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "ar.com.wolox.android.cookbook"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    // Change the "default" to whatever fits. At least one 'dimension' is required by gradle.
    flavorDimensions "default"

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] Wolmo Android Cookbook"
        }

        production {
            resValue "string", "app_name", "Wolmo Android Cookbook"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

jacocoAndroidUnitTestReport {
    excludes = defaultExcludes + ['**/*_Factory.class']
}

buildscript {
    ext.wolmo_version = '2.0.4'
    ext.support_library_version = '28.0.0'
    ext.constraint_layout_version = '1.1.3'
    ext.dagger_version = '2.13'
    ext.butterknife_version = '8.8.1'
    ext.leak_canary_version = '1.6.2'
    ext.chuck_version = '1.1.0'
    ext.powermock_version = '1.7.0'
    ext.google_firebase_version = '16.0.4'
    ext.google_play_services_version = '16.0.1'
    ext.fresco_version = '1.11.0'
    ext.nav_arch_component_version = '1.0.0-alpha07'
    ext.rxjava2_version = '2.2.0'
    ext.rxjava2_android_version = '2.1.0'
    ext.facebook_login_version = '4.39.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Wolmo
    implementation "com.github.wolox:wolmo-networking-android:$wolmo_version"

    // Android support libraries
    implementation "com.android.support:support-compat:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"

    // Constraint Layout
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Fresco
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "com.facebook.fresco:animated-gif:$fresco_version"

    // Third-party
    implementation "com.jakewharton:butterknife:$butterknife_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Testing dependencies
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-inline:2.13.0"
    testImplementation "org.assertj:assertj-core:3.9.0"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Google Play Services
    implementation "com.google.android.gms:play-services-auth:$google_play_services_version"

    // Navigation Architecture Component
    implementation "android.arch.navigation:navigation-fragment:$nav_arch_component_version"
    implementation "android.arch.navigation:navigation-ui:$nav_arch_component_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxjava2_android_version"

    // Facebook Login
    implementation "com.facebook.android:facebook-login:$facebook_login_version"

}